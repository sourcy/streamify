apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "jacoco"
apply plugin: 'checkstyle'

group = 'io.sourcy'
version = '1.7-SNAPSHOT'

description = """io.sourcy:streamify"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
    sourceSets = [project.sourceSets.main]
    toolVersion = '6.10.1'
}

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'io.javaslang', name: 'javaslang', version: '2.0.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Streamify'
                packaging 'jar'
                description 'Gluing code to connect the marvellous types from javaslang with java 8 streams and guava immutable collections.'
                url 'https://github.com/sourcy/streamify'

                scm {
                    url 'git@github.com:sourcy/streamify.git'
                    connection 'scm:git:git@github.com:sourcy/streamify.git'
                    developerConnection 'scm:git:git@github.com:sourcy/streamify.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'sLite'
                        name 'Daniel Selinger'
                        email 'd.selinger@sourcy.io'
                        organization 'Sourcy.io'
                        organizationUrl 'https://sourcy.io'
                    }
                    developer {
                        id 'awalland'
                        name 'Armin Walland'
                        email 'a.walland@sourcy.io'
                        organization 'Sourcy.io'
                        organizationUrl 'https://sourcy.io'
                    }
                }
            }
        }
    }
}

